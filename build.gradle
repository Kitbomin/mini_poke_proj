// Spring Boot 3.5.x + Gradle(Groovy) 를 사용하는 프로젝트의 build.gradle
// 목적: JPA + Web 사용의 표준 서버 애플리케이션 프로젝트 기본 설정

plugins {
    // 자바 프로젝트임을 선언함 -> Gradle이 Java 소스 컴파일, 테스트, 패키징 태스크를 인지할 수 있도록 함
    id 'java'

    // 스프링부트 실행 태스크와 자동 설정을 제공함 (프로젝트가 사용하는 Spring Boot 버전과 반드시 일치해야함)
    id 'org.springframework.boot' version '3.5.4'

    // 스프링부트 의존성 버전관리를 위임함
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example' // Maven 그룹 ID(프로젝트 패키지)
version = '0.0.1-SNAPSHOT' // 애플리케이션 버전

java {
    // 컴파일러, 런타임 JDK 버전을 고정시킴 (로컬에 17버전이 없으면 Gradle이 자동설치도 해줌)
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    // 컴파일 시에만 필요한 의존성 (EX.Lombok)
    compileOnly {
        extendsFrom annotationProcessor // 어노테이션 처리도 함께 사용할 수 있도록 설정되어있음
    }
}

repositories {
    // 의존성 중앙 저장소 - 대부분 라이브러리가 mavenCentral()에서 다운됨
    mavenCentral()
}

dependencies {
    // === Spring 스타터 === //
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'


    // Spring Web Validation 스타터
    // - 데이터 유효성 검사를 위한 의존성 추가
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spring Web Security 스타터
    // - 인증 / 보안 처리를 위한 의존성 추가
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.security:spring-security-test'

    // JWT 의존성 추가
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // === Lombok 컴파일 전용 === //
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // === DataBase 연결 === //
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // === 테스트 의존성 === //
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.2'
}

tasks.named('test') {
    useJUnitPlatform()
}
